Name     supershadow ;
PartNo   00 ;
Date     25/01/2023 ;
Revision 04 ;
Designer George Foot ;
Company  gfoot360 ;
Assembly None ;
Location None ;
Device   g22v10 ;

pin 1 = PHI0;

/* inputs */
pin 2 = SYNC;
pin 3 = RWB;
pin [ 4..9, 10, 16, 13, 11 ] = [ A6..15 ];

/* outputs */
pin [ 22, 21, 17, 18, 19, 20, 14, 23 ] = [ RSHAD, WSHAD, !BEEB, !SRHI, !SRLO, !WE, A10_BEEB, RWB_BEEB ];

/* intermediate */
pin 15 = NZP; /* nearly zero-page */

Field addrhi = [A15..A8];

NZP = addrhi:'h'0000 # addrhi:'h'0100;

stack = NZP & A8;
zp = NZP & !A8;

zpsync = zp & SYNC;

[RSHAD,WSHAD].d = zpsync & [A7,A6] # !zpsync & [RSHAD,WSHAD];
[RSHAD,WSHAD].ar = 'b'0;
[RSHAD,WSHAD].sp = 'b'0;

BEEB = RWB & !RSHAD & !stack # !RWB & !WSHAD & !stack;

SRHI = !BEEB & A15;
SRLO = !BEEB & !A15;

WE = PHI0 & !RWB;

A10_BEEB = A10 & BEEB;

RWB_BEEB = RWB # !BEEB;


